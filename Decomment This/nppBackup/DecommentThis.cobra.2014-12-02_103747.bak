"""
Removes comment formatting from the supplied text.

This;
/*+———————————————————————————————————————————————————+
  | COMMENT TITLE                                     |
  +———————————————————————————————————————————————————+
  | Comment body, Kia ora.. Take a squiz, this        |
  | snarky is as good as as a rip-off tiki tour.      |
  | Mean while, in Hamilton, Tama and James Cook      |
  | were up to no good with a bunch of sweet as L&Ps. |
  +———————————————————————————————————————————————————+*/
or this;
/*****************************************************
 * COMMENT TITLE                                     *
 *****************************************************
 * Comment body, Kia ora.. Take a squiz, this        *
 * snarky is as good as as a rip-off tiki tour.      *
 * Mean while, in Hamilton, Tama and James Cook      *
 * were up to no good with a bunch of sweet as L&Ps. *
 *****************************************************/

Becomes this:
COMMENT TITLE
Comment body, Kia ora.. Take a squiz, this 
snarky is as good as as a rip-off tiki tour. 
Mean while, in Hamilton, Tama and James Cook 
were up to no good with a bunch of sweet as L&Ps.

or this;
/*+———————————————+
  | COMMENT TITLE |
  +———————————————+*/

 Becomes this:
COMMENT TITLE
"""

@args -t:winexe

/#
NOTES
x to ! converts a nil-able value to a non nil one.
#/

use System.Windows.Forms #Enable access to the Clipboard

class Program is public
	test
		assert .allCharsInLineAreCommentCharacters('-++-') == true
		assert .allCharsInLineAreCommentCharacters('+———————————————————————+') == true
		assert .allCharsInLineAreCommentCharacters('|||||||||||||||||||||||||') == true
		assert .allCharsInLineAreCommentCharacters('|                       |') == false
		assert .allCharsInLineAreCommentCharacters('-8-8-8-') == false
		assert .allCharsInLineAreCommentCharacters('8--------------') == false
		assert .allCharsInLineAreCommentCharacters('——————————————G') == false
		assert .allCharsInLineAreCommentCharacters('/') == true
		assert .allCharsInLineAreCommentCharacters('p') == false

		Clipboard.setText('/*+——————————————+'+CobraCore.newLine+'| HELLO WORLD! |'+CobraCore.newLine+'+——————————————+*/')
		.main
		assert Clipboard.getText == 'HELLO WORLD!'

	def main has STAThread is shared
		nl = CobraCore.newLine

		input = Clipboard.getText

		if input.trim.length #i.e. there is text to process.
			lines = input.replace('\t','    ').replace(nl,'\n').split(c'\n').toList

			#Remove blank lines & trim any whitespace.
			index = 0
			while index < lines.count
				lines[index] = lines[index].trim
				if	lines[index] == String.empty
					lines.removeAt(index)
				else
					index+=1
			
			/#Or as an alternative
			for index in lines.count-1:-1:-1 #Iterate in reverse, as lists are self-adjusting
				lines[index] = lines[index].trim
				if lines[index].length == 0
					lines.removeAt(index)#/
			
			#Remove first line.
			if .allCharsInLineAreCommentCharacters(lines[0])
				lines.removeAt(0)
			
			#Remove last line.
			if .allCharsInLineAreCommentCharacters(lines[lines.count-1])
				lines.removeAt(lines.count-1)
			
			#Remove first character & last character from each line and trim.
			for index in lines.count
				lines[index] = (lines[index])[2:] #Remove the first 2 characters
				lines[index] = (lines[index])[:-2] #Remove the last 2 characters
				lines[index] = lines[index].trim
			
			#Kill any line that’s composed of all comment characters.
			for index in lines.count-1:-1:-1 #Iterate in reverse, as lists are self-adjusting
				if	.allCharsInLineAreCommentCharacters(lines[index])
					lines.removeAt(index)

			rendered = StringBuilder(lines.count)
			for line in lines
				rendered.append(line+nl)
			
			#Return rendered box
			Clipboard.setText(rendered.toString)
	#/main
	
	def allCharsInLineAreCommentCharacters(line as String) as bool is shared
		"""
		Determines if all the characters in the line are composed of comment characters.
		"""
		require
			line.trim <> String.empty
			line.trim.length <> 0		
		body
			commentCharacters = {'*','/','+','—','|','-'} #The set of “standard” “comment” characters
			
			for character in line
				if not commentCharacters.contains(character.toString)
					return false
			
			return true
	#/allCharsInLineAreCommentCharacters